Eliminating Left Recursion

Replace:

< A > ::= < A > a1
          | < A > a2
          ...
          | < A > an
          | < B >

With:
  < A > ::= < B > < A' > 

  < A' > ::= a1 < B > < A' >
             | a2 < B > < A' >
             | ...
             | an < B > < A' >
             | ""

Parser Grammar (Context Free)
=============================
< program >  ::= < statement > 
                 | < program > < statement > 
 - I know from experience that this is a while loop
   that reads statements until EOF.
 - I could convert this to right recursion.


< statement > ::= VARIABLE  < AO-Expression >
                  | LPAREN < Expression > RPAREN
                  | INTLIT < Factor' >< Term' > < Expression' >
                  | FLOATLIT < Factor' > < Term' >< Expression' >
                  | < Input >

< AO-Expression > ::= EQUAL < Expression >
                      | < Factor' >< Term' >< Expression' >

< Expression > ::= < Term > < Expression' >

< Expression' > ::= PLUS < Term > < Expression' > 
                    | MINUS < Term > < Expression' > 
                    | ""

< Term >        ::= < Factor > < Term' >

< Term' >       ::= TIMES < Factor > < Term' > 
                    | DIVIDE < Factor >  < Term' >
                    | ""

  
< Factor >      ::= < Exponent > < Factor' >

< Factor' >     ::= POW < Factor > 
                    | ""


< Exponent >    ::= LPAREN < Expression > RPAREN
                     | VARIABLE
                     | < Number > 


< Input > ::= INPUT VARIABLE


< number >  ::= INTLIT
                | FLOATLIT

 
 

Lexer Grammar (Regular)
=======================
Token       Rule
-----       -----
EQUAL       =
PLUS        +
MINUS       -
TIMES       *
DIVIDE      /
POW         ^ | **
LPAREN      (
RPAREN      )
VARIABLE    ([a-zA-Z]|_)[a-zA-Z0-9_]*
INTLIT      a string of digits
FLOATLIST   INTLIT . INTLIT
INPUT       input
